// Note: "common.gradle" in the root project contains additional initialization
//   for this project. This initialization is applied in the "build.gradle"
//   of the root project.

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

description = 'the Heart Library for jMonkeyEngine'
ext {
    artifact = 'Heart'
    version = heartVersion
    baseName = "$artifact-$version"
}

jar {
    archiveBaseName = project.ext.baseName
    exclude('**/arrow.blend')
}
javadocJar { archiveBaseName = project.ext.baseName }
sourcesJar { archiveBaseName = project.ext.baseName }
processResources.dependsOn(':HeartAssets:shapeTextures')

dependencies {
    api 'org.jmonkeyengine:jme3-core:' + jmonkeyengineVersion

    testImplementation 'junit:junit:4.13.1'
    testImplementation 'org.jmonkeyengine:jme3-desktop:' + jmonkeyengineVersion
}

build.dependsOn('pom', 'pomAsc')
task('pom', type: Copy) {
    dependsOn 'generatePomFileForMavenPublication'
    from "${buildDir}/publications/maven/pom-default.xml"
    into "${buildDir}/libs"
    rename 'pom-default.xml', baseName + '.pom'
}
task('pomAsc', type: Copy) {
    dependsOn 'signMavenPublication'
    from "${buildDir}/publications/maven/pom-default.xml.asc"
    into "${buildDir}/libs"
    rename 'pom-default.xml.asc', baseName + '.pom.asc'
}

publishToMavenLocal.doLast {
    println 'published to ' + baseName
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId artifact
            from components.java
            pom {
                description 'A general-purpose add-on library for jMonkeyEngine'
                developers {
                    developer {
                        email = 'sgold@sonic.net'
                        name = 'Stephen Gold'
                    }
                }
                licenses {
                    license {
                        distribution = 'repo'
                        name = 'New BSD (3-clause) License'
                        url = 'https://opensource.org/licenses/BSD-3-Clause'
                    }
                }
                name = 'com.github.stephengold:Heart'
                scm {
                    connection = 'scm:git:git://github.com/stephengold/Heart.git'
                    developerConnection = 'scm:git:ssh://github.com:stephengold/Heart.git'
                    url = 'http://github.com/stephengold/Heart/tree/master'
                }
                url = 'https://github.com/stephengold/Heart'
            }
            version project.ext.version
        }
    }
    repositories {
        maven {
            name = 'OSSRH'
            url = 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
        }
    }
}

signing {
    sign configurations.archives
    sign publishing.publications.maven
}
tasks.withType(Sign) {
    onlyIf { gradle.rootProject.hasProperty('signing.keyId') }
}
