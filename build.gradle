// Gradle script to build and run the Heart project

plugins {
    id 'base' // to add a "clean" task to the root project
}

ext {
    jmeTarget = '' // distinguish non-JME libraries built for specific JME releases
    //heartSnapshot = '-SNAPSHOT' // for development builds
    heartSnapshot = '' // for release builds
    heartVersion = '9.3.0' + jmeTarget + heartSnapshot
}

subprojects {
    apply from: rootProject.file('common.gradle')
}

configurations.configureEach {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds' // to disable caching of snapshots
}

tasks.register('AppChooser') {
    dependsOn ':HeartExamples:AppChooser'
    description = 'Runs the example apps.'
}

tasks.register('checkstyle') {
    dependsOn ':HeartExamples:checkstyleMain', ':HeartLibrary:checkstyleMain', \
        ':HeartLibrary:checkstyleTest', ':J3oDump:checkstyleMain'
    description = 'Checks the style of all Java sourcecode.'
}

// Register publishing tasks:

tasks.register('install') {
    dependsOn ':HeartLibrary:install'
    description = 'Installs the Maven artifacts to the local repository.'
}
tasks.register('release') {
    dependsOn ':HeartLibrary:release'
    description = 'Stages the Maven artifacts to the Central Publisher Portal.'
}
