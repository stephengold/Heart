// Note: "common.gradle" in the root project contains additional initialization
//   for this project. This initialization is applied in the "build.gradle"
//   of the root project.

plugins {
    id 'application'
}

mainClassName = 'jme3utilities.mesh.test.TestSolidMeshes'
if (!hasProperty('mainClass')) {
    ext.mainClass = mainClassName
}
jar.manifest.attributes('Main-Class': mainClassName)

dependencies {
    implementation ('com.github.stephengold:Acorus:0.9.10') {
        exclude group: 'com.github.stephengold', module: 'Heart'
    }
    runtimeOnly 'org.jmonkeyengine:jme3-desktop:' + jme3Version
    runtimeOnly 'org.jmonkeyengine:jme3-effects:' + jme3Version
    runtimeOnly 'org.jmonkeyengine:jme3-lwjgl3:' + jme3Version
    runtimeOnly 'org.jmonkeyengine:jme3-testdata:' + jme3Version

    // HeartExamples doesn't use jme3-jogg nor jme3-plugins
    //  -- they are included solely to avoid warnings from AssetConfig.
    runtimeOnly 'org.jmonkeyengine:jme3-jogg:' + jme3Version
    runtimeOnly 'org.jmonkeyengine:jme3-plugins:' + jme3Version

    // LWJGL 3.3 dependencies that were omitted from JME v3.5:
    runtimeOnly 'org.lwjgl:lwjgl:' + lwjgl3Version + ':natives-macos-arm64'
    runtimeOnly 'org.lwjgl:lwjgl-glfw:' + lwjgl3Version + ':natives-macos-arm64'
    runtimeOnly 'org.lwjgl:lwjgl-jemalloc:' + lwjgl3Version + ':natives-macos-arm64'
    runtimeOnly 'org.lwjgl:lwjgl-openal:' + lwjgl3Version + ':natives-macos-arm64'
    runtimeOnly 'org.lwjgl:lwjgl-opengl:' + lwjgl3Version + ':natives-macos-arm64'

    //implementation 'com.github.stephengold:Heart:' + heartVersion
    implementation project(':HeartLibrary')
}

// cleanup tasks:

clean.dependsOn('cleanDLLs', 'cleanDyLibs', 'cleanLogs', 'cleanSOs', 'cleanWrittenAssets')

task cleanDLLs(type: Delete) { // extracted Windows native libraries
    delete fileTree(dir: '.', include: '*.dll')
}
task cleanDyLibs(type: Delete) { // extracted macOSX native libraries
    delete fileTree(dir: '.', include: '*.dylib')
}
task cleanLogs(type: Delete) { // JVM crash logs
    delete fileTree(dir: '.', include: 'hs_err_pid*.log')
}
task cleanSOs(type: Delete) { // extracted Linux and Android native libraries
    delete fileTree(dir: '.', include: '*.so')
}
task cleanWrittenAssets(type: Delete) { // Acorus output directory
    delete 'Written Assets'
}

// tasks to run the example apps:

task TestBoundsVisualizer(type: JavaExec) {
    mainClass = 'jme3utilities.debug.test.TestBoundsVisualizer'
}
task TestPas(type: JavaExec) {
    mainClass = 'jme3utilities.debug.test.TestPas'
}
task TestSkeletonVisualizer(type: JavaExec) {
    mainClass = 'jme3utilities.debug.test.TestSkeletonVisualizer'
}

task TestGenerator(type: JavaExec) {
    mainClass = 'jme3utilities.math.test.TestGenerator'
}

task LoopMeshTest(type: JavaExec) {
    mainClass = 'jme3utilities.mesh.test.LoopMeshTest'
}
task TestMergeMeshes(type: JavaExec) {
    mainClass = 'jme3utilities.mesh.test.TestMergeMeshes'
}
task TestSolidMeshes(type: JavaExec) {
    mainClass = 'jme3utilities.mesh.test.TestSolidMeshes'
}

task TestContrast(type: JavaExec) {
    mainClass = 'jme3utilities.test.TestContrast'
}
task TestMulticolor(type: JavaExec) {
    mainClass = 'jme3utilities.test.TestMulticolor'
}
task TestMySpatial(type: JavaExec) {
    mainClass = 'jme3utilities.test.TestMySpatial'
}
task TestNameGenerator(type: JavaExec) {
    mainClass = 'jme3utilities.test.TestNameGenerator'
}
task TestWireframe(type: JavaExec) {
    mainClass = 'jme3utilities.test.TestWireframe'
}