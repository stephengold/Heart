// Gradle script to build the Heart/HeartExamples sub-project

// Note: "common.gradle" in the root project contains additional initialization
//   for this project. This initialization is applied in the "build.gradle"
//   of the root project.

plugins {
    id 'application' // to build a Java application
}

if (!hasProperty('mainClass')) {
    ext.mainClass = 'jme3utilities.test.AppChooser'
}
jar.manifest.attributes('Main-Class': 'jme3utilities.test.AppChooser')

dependencies {
    implementation 'org.apache.commons:commons-exec:1.3'
    implementation ('com.github.stephengold:Acorus:1.0.0') {
        exclude group: 'com.github.stephengold', module: 'Heart'
    }
    runtimeOnly 'org.jmonkeyengine:jme3-desktop:' + jme3Version
    runtimeOnly 'org.jmonkeyengine:jme3-effects:' + jme3Version
    implementation 'org.jmonkeyengine:jme3-lwjgl3:' + jme3Version
    runtimeOnly 'org.jmonkeyengine:jme3-testdata:' + jme3Version // for Jaime

    // HeartExamples doesn't use jme3-jogg nor jme3-plugins
    //  -- they are included solely to avoid warnings from AssetConfig.
    runtimeOnly 'org.jmonkeyengine:jme3-jogg:' + jme3Version
    runtimeOnly 'org.jmonkeyengine:jme3-plugins:' + jme3Version

    //implementation 'com.github.stephengold:Heart:' + heartVersion // for published library
    implementation project(':HeartLibrary') // for latest library build
}

import org.gradle.nativeplatform.platform.internal.DefaultNativePlatform
Boolean isMacOS = DefaultNativePlatform.currentOperatingSystem.isMacOsX()

tasks.withType(JavaExec) { // Java runtime options:
    if (isMacOS) {
        jvmArgs '-XstartOnFirstThread'
    } else {
        args '--showSettingsDialog'
    }
    dependsOn 'installDist'
}

// cleanup tasks:

clean.dependsOn('cleanDLLs', 'cleanDyLibs', 'cleanLogs', 'cleanSandbox', 'cleanSOs')

tasks.register('cleanDLLs', Delete) { // extracted Windows native libraries
    delete fileTree(dir: '.', include: '*.dll')
}
tasks.register('cleanDyLibs', Delete) { // extracted macOS native libraries
    delete fileTree(dir: '.', include: '*.dylib')
}
tasks.register('cleanLogs', Delete) { // JVM crash logs
    delete fileTree(dir: '.', include: 'hs_err_pid*.log')
}
tasks.register('cleanSandbox', Delete) { // Acorus sandbox
    delete 'Written Assets'
}
tasks.register('cleanSOs', Delete) { // extracted Linux and Android native libraries
    delete fileTree(dir: '.', include: '*.so')
}

// tasks to run specific apps:

tasks.register('AppChooser', JavaExec) {
    mainClass = 'jme3utilities.test.AppChooser'
}

tasks.register('TestBoundsVisualizer', JavaExec) {
    mainClass = 'jme3utilities.debug.test.TestBoundsVisualizer'
}
tasks.register('TestPas', JavaExec) {
    mainClass = 'jme3utilities.debug.test.TestPas'
}
tasks.register('TestSkeletonVisualizer', JavaExec) {
    mainClass = 'jme3utilities.debug.test.TestSkeletonVisualizer'
}

tasks.register('TestGenerator', JavaExec) {
    mainClass = 'jme3utilities.math.test.TestGenerator'
}

tasks.register('LoopMeshTest', JavaExec) {
    mainClass = 'jme3utilities.mesh.test.LoopMeshTest'
}
tasks.register(' TestMergeMeshes', JavaExec) {
    mainClass = 'jme3utilities.mesh.test.TestMergeMeshes'
}
tasks.register(' TestRotate', JavaExec) {
    mainClass = 'jme3utilities.mesh.test.TestRotate'
}
tasks.register('TestSolidMeshes', JavaExec) {
    mainClass = 'jme3utilities.mesh.test.TestSolidMeshes'
}
tasks.register('TestSubdivide', JavaExec) {
    mainClass = 'jme3utilities.mesh.test.TestSubdivide'
}

tasks.register('TestContrast', JavaExec) {
    mainClass = 'jme3utilities.test.TestContrast'
}
tasks.register('TestMulticolor', JavaExec) {
    mainClass = 'jme3utilities.test.TestMulticolor'
}
tasks.register('TestMySpatial', JavaExec) {
    mainClass = 'jme3utilities.test.TestMySpatial'
}
tasks.register('TestNameGenerator', JavaExec) {
    mainClass = 'jme3utilities.test.TestNameGenerator'
}
tasks.register('TestWireframe', JavaExec) {
    mainClass = 'jme3utilities.test.TestWireframe'
}