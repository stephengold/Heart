// Gradle script to build the J3oDump subproject of "Heart"

// Note: "common.gradle" in the root project contains additional initialization
//   for this project. This initialization is applied in the "build.gradle"
//   of the root project.

plugins {
    id 'application' // to build JVM applications
}

tasks.withType(JavaCompile).configureEach { // Java compile-time options:
    options.deprecation = true
}

application {
    mainClass = 'jme3utilities.cli.J3oDump'
}
jar.manifest.attributes('Main-Class': application.mainClass)

tasks.register('j3odump', JavaExec) {
    args('-r', '../../jmonkeyengine/jme3-testdata/src/main/resources', '-x', 'Models/Jaime/Jaime.j3o')
    debug = true
    mainClass = 'jme3utilities.cli.J3oDump'
}

dependencies {
    implementation(libs.jme3.desktop)
    implementation(libs.jme3.plugins)

    //implementation 'com.github.stephengold:Heart:' + heartVersion // for published library
    implementation project(':HeartLibrary') // for latest sourcecode
}

distTar.dependsOn(':HeartLibrary:build')
distZip.dependsOn(':HeartLibrary:build')
startScripts.dependsOn(':HeartLibrary:build')
